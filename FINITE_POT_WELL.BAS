100 REM A PROGRAM TO SOLVE THE DIFFERENTIAL EQUATION FOR THE FINITE POTENTIAL WELL
110 PRINT "ENTER BOUNDARY VALUES"
120 INPUT  PROMPT "ENTER PSY_INIT: ": PSI_INIT
130 INPUT  PROMPT "ENTER SLOPE_INIT: ": SLOPE_INIT
140 INPUT  PROMPT "ENTER BETA: ": BETA
150 INPUT  PROMPT "ENTER E (E<1 FOR BOUND STATES AND E>1 OTHERWISE): ": E
160 LET X = 0
170 LET PSI = PSI_INIT
180 LET SLOPE = SLOPE_INIT
185 LET XMAX = 2
186 LET YMAX = 2
190 SET WINDOW -XMAX, XMAX, -YMAX, YMAX
200 DRAW GRID
210 LET DX = 0.000002
220 FOR I = 0 TO ABS(XMAX/DX)
230    LET Z = PSI + DX*SLOPE
240    PLOT LINES: X, PSI; X+DX, Z
250    LET X = X + DX
260    IF ABS(X)>0.5 THEN 
270       LET SLOPE = SLOPE + DX*BETA*(1-E)*PSI
280    ELSEIF ABS(X)<0.5 THEN 
290       LET SLOPE = SLOPE + DX*BETA*(- E)*PSI 
300    ELSE
310       LET SLOPE = SLOPE + DX*BETA*(0.5-E)*PSI
320    END IF
330    LET PSI = Z
340 NEXT I
350 IF DX > 0 THEN
360    LET DX = -DX
370    LET X = 0
380    LET PSI = PSI_INIT
390    LET SLOPE = SLOPE_INIT
395    GOTO 220
400 END IF
410 END
  
